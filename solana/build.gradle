plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'maven-publish'
}

group 'com.solana'
version '2.1.1'

android {
    compileSdkVersion 33
    buildToolsVersion '30.0.2'
    android.buildFeatures.buildConfig true
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 1
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // read RPC URL properties fro testing
        def defaultRpcUrl = project.properties["testing.rpc.defaultUrl"]
        def rpcUrl = project.properties["rpcUrl"] ?: defaultRpcUrl

        def useLocalValidator = project.properties["localValidator"].toBoolean()
        def localRpcUrl = project.properties["testing.rpc.localUrl"]
        if (useLocalValidator && localRpcUrl != null) rpcUrl = localRpcUrl

        // set RPC URL (used in testing)
        buildConfigField 'String', 'RPC_URL', "\"${rpcUrl.toString().replace('"', '')}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    namespace 'com.solana.solana'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'org.bitcoinj:bitcoinj-core:0.16.2'
    implementation 'net.i2p.crypto:eddsa:0.3.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'

}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}